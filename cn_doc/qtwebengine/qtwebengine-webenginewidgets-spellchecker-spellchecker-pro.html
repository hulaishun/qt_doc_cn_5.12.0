<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE html>
<html lang="en">
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
  <title>spellchecker.pro Example File | Qt WebEngine 5.12</title>
  <link rel="stylesheet" type="text/css" href="style/offline-simple.css" />
  <script type="text/javascript">
    document.getElementsByTagName("link").item(0).setAttribute("href", "style/offline.css");
    // loading style sheet breaks anchors that were jumped to before
    // so force jumping to anchor again
    setTimeout(function() {
        var anchor = location.hash;
        // need to jump to different anchor first (e.g. none)
        location.hash = "#";
        setTimeout(function() {
            location.hash = anchor;
        }, 0);
    }, 0);
  </script>
</head>
<body>
<div class="header" id="qtdocheader">
  <div class="main">
    <div class="main-rounded">
      <div class="navigationbar">
        <table><tr>
<td ><a href="../qtdoc/index.html">Qt 5.12</a></td><td ><a href="qtwebengine-index.html">Qt WebEngine</a></td><td ><a href="qtwebengine-webenginewidgets-spellchecker-example.html">WebEngine Widgets Spellchecker Example</a></td><td >spellchecker.pro Example File</td></tr></table><table class="buildversion"><tr>
<td id="buildversion" width="100%" align="right">Qt5.12.0参考文档</td>
        </tr></table>
      </div>
    </div>
<div class="content">
<div class="line">
<div class="content mainContent">
<div class="sidebar"><div class="sidebar-content" id="sidebar-content"></div></div>
<h1 class="title">spellchecker.pro Example File</h1>
<span class="subtitle">webenginewidgets/spellchecker/spellchecker.pro</span>
<!-- $$$webenginewidgets/spellchecker/spellchecker.pro-description -->
<div class="descr"> <a name="details"></a>
<pre class="cpp">

  include($$QTWEBENGINE_OUT_ROOT/src/core/qtwebenginecore-config.pri) # workaround for QTBUG-68093
  QT_FOR_CONFIG += webenginecore

  TEMPLATE = app
  TARGET = spellchecker
  QT += webenginewidgets

  !qtConfig(webengine-spellchecker) {
      error(&quot;Qt WebEngine compiled without spellchecker support, this example will not work.&quot;)
  }

  qtConfig(webengine-native-spellchecker) {
      error(&quot;Spellcheck example can not be built when using native OS dictionaries.&quot;)
  }

  HEADERS += \
      webview.h

  SOURCES += \
      main.cpp \
      webview.cpp

  RESOURCES += \
      data/spellchecker.qrc

  DISTFILES += \
      dict/en/README.txt \
      dict/en/en-US.dic \
      dict/en/en-US.aff \
      dict/de/README.txt \
      dict/de/de-DE.dic \
      dict/de/de-DE.aff

  target.path = $$[QT_INSTALL_EXAMPLES]/webenginewidgets/spellchecker
  INSTALLS += target

  qtPrepareTool(CONVERT_TOOL, qwebengine_convert_dict)

  debug_and_release {
      CONFIG(debug, debug|release): DICTIONARIES_DIR = debug/qtwebengine_dictionaries
      else: DICTIONARIES_DIR = release/qtwebengine_dictionaries
  } else {
      DICTIONARIES_DIR = qtwebengine_dictionaries
  }

  dict_base_paths = en/en-US de/de-DE
  for (base_path, dict_base_paths) {
      dict.files += $$PWD/dict/$${base_path}.dic
  }

  dictoolbuild.input = dict.files
  dictoolbuild.output = $${DICTIONARIES_DIR}/${QMAKE_FILE_BASE}.bdic
  dictoolbuild.depends = ${QMAKE_FILE_PATH}/${QMAKE_FILE_BASE}.aff
  dictoolbuild.commands = $${CONVERT_TOOL} ${QMAKE_FILE_IN} ${QMAKE_FILE_OUT}
  dictoolbuild.name = Build ${QMAKE_FILE_IN_BASE}
  dictoolbuild.CONFIG = no_link target_predeps
  QMAKE_EXTRA_COMPILERS += dictoolbuild

  # When the example is compiled as a bundle, WebEngine expects to find the dictionaries in
  # bundle.app/Contents/Resources/qtwebengine_dictionaries
  macos:app_bundle {
      for (base_path, dict_base_paths) {
          base_path_splitted = $$split(base_path, /)
          base_name = $$last(base_path_splitted)
          binary_dict_files.files += $${DICTIONARIES_DIR}/$${base_name}.bdic
      }
      binary_dict_files.path = Contents/Resources/$$DICTIONARIES_DIR
      QMAKE_BUNDLE_DATA += binary_dict_files
  }

</pre>
</div>
<!-- @@@webenginewidgets/spellchecker/spellchecker.pro -->
        </div>
       </div>
   </div>
   </div>
</div>
<div class="footer">
   <p>
   <acronym title="Copyright">&copy;</acronym> 2018 The Qt Company Ltd.
   Documentation contributions included herein are the copyrights of
   their respective owners.<br/>    The documentation provided herein is licensed under the terms of the    <a href="http://www.gnu.org/licenses/fdl.html">GNU Free Documentation    License version 1.3</a> as published by the Free Software Foundation.<br/>    Qt and respective logos are trademarks of The Qt Company Ltd.     in Finland and/or other countries worldwide. All other trademarks are property
   of their respective owners. </p>
</div>
</body>
</html>
